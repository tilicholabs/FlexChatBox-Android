apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = group
    PUBLISH_VERSION = version
    PUBLISH_ARTIFACT_ID = 'flexchatbox-android'
    PUBLISH_DESCRIPTION = 'FlexChatBox Android SDK'
    PUBLISH_URL = 'https://github.com/tilicholabs/FlexChatBox-Android/'
    PUBLISH_LICENSE_NAME = 'MIT License'
    PUBLISH_LICENSE_URL = 'https://github.com/tilicholabs/FlexChatBox-Android/blob/main/LICENSE'
    PUBLISH_SCM_CONNECTION = 'scm:git:github.com/tilicholabs/FlexChatBox-Android.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION = 'scm:git:ssh://github.com/tilicholabs/FlexChatBox-Android.git'
    PUBLISH_SCM_URL = 'https://github.com/tilicholabs/FlexChatBox-Android/tree/main'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin('com.android.library')) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = PUBLISH_URL
                    licenses {
                        license {
                            name = PUBLISH_LICENSE_NAME
                            url = PUBLISH_LICENSE_URL
                        }
                    }
                    developers {
                        developer {
                            id = 'swamy'
                            name = 'Demudu Swamy'
                            email = 'swamyrallapalli@gmail.com'
                        }

                        developer {
                            id = 'sravan.kumar'
                            name = 'Sravan Kumar'
                            email = 'sravankaraka.149@gmail.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = PUBLISH_SCM_CONNECTION
                        developerConnection = PUBLISH_SCM_DEVELOPER_CONNECTION
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.key"] = rootProject.ext["signing.key"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
